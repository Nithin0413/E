// MODULE 1
//print "MOD 1";
alias function_number R1;
alias current_pid R2;

// Free User Area Page Function
if( function_number ==2 ) then
    alias user_area R3;
    user_area = [PROCESS_TABLE + current_pid*16 +11 ];
    // Release Page
    multipush(R1,R2,R3);
    R1=2;
    R2=user_area;
    call MOD_2 ;
    multipop(R1,R2,R3);
    return;
endif ;

// Exit Process
if( function_number== 3) then
    // Free page Table
    multipush(R1,R2,R3);
    R1=4;
    call MOD_1 ;
    multipop(R1,R2,R3);

    //Free User Area Page
	multipush(R1,R2,R3);
	R1 = 2;
	call MOD_1; 
	multipop(R1,R2,R3);
    // Terminating the current process
    [PROCESS_TABLE +current_pid*16+4]=TERMINATED;
    return;
endif;

// Free Page Table Function
if( function_number == 4 ) then
    alias ptbr R3;
    ptbr = PTBR + current_pid*20;
    // Invalidate the page table entries for the library
    [ptbr +0]=-1;
    [ptbr +1]="0000";
    [ptbr +2]=-1;
    [ptbr +3]="0000";
    
    alias it R4;
    it=4;
    while(it<20) do
        if([ptbr +it]!=-1) then 
            // Release Page
            multipush(R1,R2,R3,R4);
            R1=2;
            R2=[ptbr+it];
            call MOD_2 ;
            multipop(R1,R2,R3,R4);
            [ptbr + it] = -1;
			[ptbr + it + 1] = "0000";
        endif;
        it=it+2;
    endwhile;
    return;
endif ;

return;

