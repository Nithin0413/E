//FILE MANAGER MODULE
//print "MOD3";

alias function_num R1;
alias curretn_pid R2;



//buffered Read
if(function_num == 2) then
	
	alias blockNum R3;
	alias offset R4;
	alias addr R5;
	alias pageNum R6;
	alias bufferNum R7;
	
	
	
	bufferNum = blockNum % 4;
	pageNum = 71 + bufferNum;
	
	//acquire buffer
	multipush(R1,R2,R3,R4,R5,R6,R7);
	R1 = 1;
	R3 = bufferNum;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7);
	
	
	
	alias buffer_entry R8;
	buffer_entry = BUFFER_TABLE + bufferNum*4;
	
	// Check If some other block was loaded into buffer
	if([buffer_entry] != blockNum) then
		
		if([buffer_entry + 1] == 1) then
			
			multipush(R1,R2,R3,R4,R5,R6,R7,R8);
			R1 = 1;
			R3 = pageNum;
			R4 = [buffer_entry];
			call MOD_4;
			multipop(R1,R2,R3,R4,R5,R6,R7,R8);
			
			[buffer_entry + 1] = 0;
		endif;
		
		multipush(R1,R2,R3,R4,R5,R6,R7,R8);
		R1 = 2;
		R4 = blockNum;
		R3 = pageNum;
		call MOD_4;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8);
		[buffer_entry] = blockNum;
	endif;
	
	//Copy Data to Physical address
	[[PTBR + 2 * (addr/512)] * 512 + (addr%512)] = [pageNum*512 + offset];

	//Release Buffer
	multipush(R1,R2,R3,R4,R5,R6,R7,R8);
	R1 = 2;
	R3 = bufferNum;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6,R7,R8);
	return;
endif;

//OPEN FUNCTION
if(function_num == 3) then
	
	alias filename R3;
	alias inode_table_entry R4;
	alias inode_index R5;
	

	
	//Searching Inode Table for file
	inode_index = 0;

	while(inode_index < MAX_FILE_NUM) do
		inode_table_entry = INODE_TABLE + (inode_index * 16);
		if([inode_table_entry + 1] == filename) then
			break;
		endif;
		inode_index = inode_index + 1;
	endwhile;
	
	
	//file not found
	if(inode_index == MAX_FILE_NUM) then
		R0 = -1;
		return;
	endif;
	
	//locking inode
	multipush(R1,R2,R3,R4,R5);
	R1 = 4;
	R3 = inode_index;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5);
	
	
	//locking failed
	if(R0 == -1) then
		return;
	endif;
	
	//If file is of the type EXEC then release inode
	if([inode_table_entry] == EXEC) then
		multipush(R1,R2,R3,R4,R5);
		R1 = 5;
		R3 = inode_index;
		call MOD_0;
		multipop(R1,R2,R3,R4,R5);
		R0 = -1;
		return;
	endif;
	
	
	//find free entry in the open file table
	alias open_ft_inode_index R6;
	alias open_ft_entry R7;
	open_ft_inode_index = 0;
	while(open_ft_inode_index < MAX_OPENFILE_NUM) do
		if([OPEN_FILE_TABLE + open_ft_inode_index*4] == -1) then
			open_ft_entry = OPEN_FILE_TABLE + open_ft_inode_index*4;
			break;
		endif;
		open_ft_inode_index = open_ft_inode_index + 1;
	endwhile;
	
	//if no free entry in open file table
	if(open_ft_inode_index == MAX_OPENFILE_NUM) then
		multipush(R1,R2,R3,R4,R5,R6);
		R1 = 5;
		R3 = inode_index;
		call MOD_0;
		multipop(R1,R2,R3,R4,R5,R6);
		
		R0 = -2;
		return;
	endif;
	
	
	//updating file status table count and open file table inode inode_index field
	if(filename == "root") then
		[open_ft_entry] = INODE_ROOT;
	else
		if([FILE_STATUS_TABLE + 4*inode_index + 1] == -1) then
			[FILE_STATUS_TABLE + 4*inode_index + 1] = 1;
		else
			[FILE_STATUS_TABLE + 4*inode_index + 1] = [FILE_STATUS_TABLE + 4*inode_index + 1] + 1;
		endif; 
		[open_ft_entry] = inode_index;
	endif;
	
	
	
	//updating open file table entries
	[open_ft_entry + 1] = 1;
	[open_ft_entry + 2] = 0;
	
	
	//releasing inode
	multipush(R1,R2,R3,R4,R5,R6);
	R1 = 5;
	R3 = inode_index;
	call MOD_0;
	multipop(R1,R2,R3,R4,R5,R6);
	
	
	
	R0 = open_ft_inode_index;
	return;
	
endif;

//close
if(function_num == 4) then
	alias open_ft_inode_index R3;
	alias inode_inode_index R4;
	alias open_ft_entry R5;
	
	open_ft_entry = OPEN_FILE_TABLE + open_ft_inode_index*4;
	
	inode_inode_index = [open_ft_entry];
	[open_ft_entry + 1] = [open_ft_entry + 1] - 1;
	
	//if open instance count is 0
	if([open_ft_entry + 1] == 0) then
		[open_ft_entry] = -1;
		[open_ft_entry + 1] = -1;
		[open_ft_entry + 2] = -1;
		[open_ft_entry + 3] = -1;
		
		if([INODE_TABLE + inode_inode_index*16] != "root") then
			[FILE_STATUS_TABLE + 4*inode_inode_index + 1] = [FILE_STATUS_TABLE + 4*inode_inode_index + 1] - 1;
			if([FILE_STATUS_TABLE + 4*inode_inode_index + 1] == 0) then
				[FILE_STATUS_TABLE + 4*inode_inode_index + 1] = -1;
			endif;
		endif;
	endif;
	
	return;
	
	
	
endif;


